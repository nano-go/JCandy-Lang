import "unit" as unit

class ThreadTest : unit.TestCase {

	fun import_test() {
		var t1 = Thread.start(lambda -> {
			import "stream" as stream_1
		})
		var t2 = Thread.start(lambda -> {
			import "stream" as stream_2
		})
		t1.join()
		t2.join()
		@assertTrue(stream_1 == stream_2)
	}

	fun closure_test() {
		var index = 0
		Thread.start(-> {
			index += 1
		}).join()
		@assertEquals(1, index) 
	}

	fun error_test() {
		fun raiseAnError() {
			raise Error("I'm an error.")
		}
		fun callRaiseAnError() {
			raiseAnError()
		}
		fun catchError() {
			try {
				callRaiseAnError()
			} intercept Error as e {
				var stackTrace = e.getStackTraceElements()
				@assertEquals(3, stackTrace.length())
				@assertEquals("raiseAnError", stackTrace[0].getFrameName())
				@assertEquals("callRaiseAnError", stackTrace[1].getFrameName())
			}
		}
		Thread.start(catchError).join()
	}

	fun threadAttr_test() {
		var i = 0
		var t = Thread(-> i += 1)
		@assertNotNull(t.name)
		@assertTrue(t.id > 0)
		@assertFalse(t.isDaemon)
		@assertFalse(t.isAlive)
		@assertFalse(t.isInterrupted)

		@expectedErrorRaised(AttributeError, -> t.id = 0)
		@expectedErrorRaised(AttributeError, -> t.isAlive = 0)
		@expectedErrorRaised(AttributeError, -> t.isInterrupted = 0)
	}
}
